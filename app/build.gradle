def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion versions.compileSdk

    defaultConfig {
        applicationId "com.erdemtsynduev.vgikgagsbutton"

        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        applicationVariants.all { variant ->
            variant.buildConfigField "String", "AD_ID_KEY", "\"" + keystoreProperties['adIdKey'] + "\""
            variant.buildConfigField "String", "AD_ID_INTERSTITIAL", "\"" + keystoreProperties['adIdInterstitial'] + "\""
        }
        release {
            minifyEnabled false
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation deps.support.appCompat
    implementation deps.support.constraintLayout
    implementation deps.support.design

    implementation deps.firebase.core
    implementation deps.firebase.ads
    implementation deps.crashlytics

    implementation deps.butterknife.core
    annotationProcessor deps.butterknife.compiler

    implementation deps.sdpAndroid

    testImplementation deps.junit
    androidTestImplementation deps.testRunner
    androidTestImplementation(deps.espresso.core) {
        exclude group: 'com.google.code.findbugs'
    }
}

apply plugin: 'com.google.gms.google-services'
